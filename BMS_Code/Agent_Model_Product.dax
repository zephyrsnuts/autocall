EVALUATE
SUMMARIZECOLUMNS(
	//Columns
	'Dim - Calendar'[Fiscal Week],
	'Hier - Channel'[Region Desc],
	'Hier - Channel'[SubRegion Desc],
	'Hier - Product'[Product Type],
	'Hier - Product'[Product Group],
	'Hier - Agent'[Group Name],
	'Hier - Agent'[BMS TS Commercial Region],
	'Hier - Agent'[Sub-Region Name],
	'Hier - Agent'[Location Name],
	
	//Filters
    FILTER (
		VALUES ( 'Dim - Calendar'[Fiscal Quarter Lag] ),
		( 'Dim - Calendar'[Fiscal Quarter Lag] >= -5 )
    ),
    FILTER (
		VALUES ( 'Dim - Calendar'[Fiscal Week Lag] ),
		( 'Dim - Calendar'[Fiscal Week Lag] <= -1 )
    ),
    FILTER (
		VALUES ( 'Hier - Agent'[Category Name] ),
		( 'Hier - Agent'[Category Name] = "Tech Support" )
    ),
    FILTER (
		VALUES ( 'Hier - Product'[Product Type] ),
		( 'Hier - Product'[Product Type] = "Enterprise Solution Group PBU" )
    ),
    FILTER (
    	VALUES ('Fact - CE Responses'[CE Warranty Status] ),
    	( 'Fact - CE Responses'[CE Warranty Status] <> "OOW" )
    ),
    FILTER (
    	VALUES ('Fact - CE Responses'[Response Status] ),
    	( 'Fact - CE Responses'[Response Status] = "Completed" )
    ),
    FILTER (
    	VALUES ( 'Fact - CE Disposition'[Survey ID] ),
    	( 'Fact - CE Disposition'[Survey ID] <> "3076" )
	),
    FILTER (
    	VALUES ( 'Fact - Case SFDC'[SFDC Case Type] ),
    	( 'Fact - Case SFDC'[SFDC Case Type] <> "Internal" )
	),
    

	//Expressions
	"RDR",[Repeat Dispatch 7 Day Lag %],
	"RDR_NUM",[Repeat Dispatch 7 day Numerator],
	"RDR_DENOM",[Repeat Dispatch Denominator],
	
	"TTC",[TTC – SFDC + DELTA],
	"TTC_NUM",CALCULATE(
					    SUM('Fact - Case SFDC'[Time to Complete])/86400
					    , 'Fact - Case SFDC'[Quick Contact Flag]<>"Y"
					    , TREATAS(VALUES('Dim - Calendar'[DATE]),'Fact - Case SFDC'[Complete Date])
					    , TREATAS(VALUES('Hier - Agent'[Agent Key]),'Fact - Case SFDC'[Agent_Owner_Key_ClosedDate])
					  )
					  + CALCULATE(
					    SUM('Fact - SR'[CASE AGE])
					    , 'Fact - SR'[SFDC Exlusion Flag]=0
					    , TREATAS(VALUES('Dim - Calendar'[DATE]),'Fact - SR'[Activity Close Date])
					    , TREATAS(VALUES('Hier - Agent'[Agent Key]),'Fact - SR'[OwnerKey_closedate])
					  ),
	"TTC_DENOM",[Cases Completed (Owner) SFDC + DELTA],
	
	"TTR",[TTR – SFDC + DELTA],
	"TTR_NUM",CALCULATE(
					    SUM('Fact - Case SFDC'[Time to Resolve])/86400
					    , 'Fact - Case SFDC'[Quick Contact Flag]<>"Y"
					    , TREATAS(VALUES('Dim - Calendar'[DATE]),'Fact - Case SFDC'[Complete Date])
					    , TREATAS(VALUES('Hier - Agent'[Agent Key]),'Fact - Case SFDC'[Agent_Owner_Key_ClosedDate])
					  )
					  + CALCULATE(
					    [TTR (Days)]
					    , 'Fact - SR'[SFDC Exlusion Flag]=0
					    , TREATAS(VALUES('Dim - Calendar'[DATE]),'Fact - SR'[Activity Close Date])
					    , TREATAS(VALUES('Hier - Agent'[Agent Key]),'Fact - SR'[OwnerKey_closedate])
					  ),
	"TTR_DENOM", 'Fact - Case SFDC'[Cases Completed (Owner) SFDC] 
					+ CALCULATE('Fact - SR'[SR Resolved]
							, 'Fact - SR'[SFDC Exlusion Flag]=0
							, TREATAS(VALUES('Dim - Calendar'[DATE]), 'Fact - SR'[Activity Close Date])
							, TREATAS(VALUES('Hier - Agent'[Agent Key]), 'Fact - SR'[Agent_Owner_Key])
							)
)